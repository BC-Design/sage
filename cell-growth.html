<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">

<html>
<head>
<!-- Local files containing the website icon and stylesheet. -->
<link rel="shortcut icon" type="image/x-icon" href="favicon.ico">
<link rel="stylesheet" type="text/css" href="theme.css">

<!-- Imports needed to use the Sage cell server. -->
<script type="text/javascript" src="http://aleph2.sagemath.org/static/jquery.min.js"></script>
<script type="text/javascript" src="http://aleph2.sagemath.org/embedded_sagecell.js"></script>
<script type="text/javascript" src="sagecell.js"></script>
<script type="text/javascript" src="sage-text.js"></script>
<script type="text/javascript" src="ganalytics.js"></script>

<!-- Additional CSS customizations --> 
<style type="text/css"></style>

<!-- Page title --> 
<title>Sage Math - Cell Growth</title>
</head>


<body>
<h1>Sage Math Tutorial - Cell Growth</h1>
<script type="text/javascript">sageHome()</script>

<p>This page contains interactive Sage widgets - edit and re-evaluate them!</p>

<p>The demos below span several related topics:
<ol>
  <li><a href="#cells">Cell Growth</a>
  <li><a href="#cstr">The CSTR</a>
  <li><a href="#cstr_rxn">CSTR with Reaction</a>
  <li><a href="#cstr_cells">CSTR with Cell Growth</a>
  <li><a href="#conical">Draining a Conical Tank</a>
  <li><a href="#heat">Intercooled CSTR</a>
</ol>
</p>

<h2><a id="cells">Cell Growth</a></h2>

<p>If your are solving a simple 1st or 2nd order ordinary differential equation, you can use desolve() to get an analytical solution.  Here we are simulating a system in which cells 'X' grow at a rate proportional to their number, resulting in unbounded exponential growth:</p>

<!-- Create the Sage cell.  
     You can put this anywhere, e.g., inside tables, etc. -->
<div class="sagecell-plot" id="sagecell1"><script type="application/sage">
# First, define the cell growth differential equation
var('X t')
X = function('X',t)
diffeqs = diff(X,t) == X

# Next, solve it and show the solution
solution(t) = desolve(diffeqs,[X,t],[0,1])
show (solution(t))
plot(solution(t),(t,0,1),figsize=(6,4))
</script></div>

<p>For initial value problems featuring systems of first-order differential equations, try the numerical solver desolve_system_rk4().  In this example, we are simulating a system in which cells 'X' are increasing in concentration by consuming a substrate 'S'; the cells' growth rate 'mu' obeys saturation kinetics with respect to substrate concentration:</p>

<div class="sagecell-plot" id="sagecell2"><script type="application/sage">
## Variables
var('mu t X S')
## Equations
mu = (S)/(1+S)
des = [mu*X, -mu*X]
sol = desolve_system_rk4(des, [X,S], ics=[0,0.1,10], ivar=t)
## The data needs some post-processing before we can graph it:
solX = []; solS = []
for i in range(0,len(sol)):
    solX.append([sol[i][0],sol[i][1]])
    solS.append([sol[i][0],sol[i][2]])
a = plot([],figsize=(6,4))
a += list_plot(solX, color='green', legend_label='[cells]')
a += list_plot(solS, color='blue', legend_label='[substrate]')
a.axes_labels(['time','concentration'])
show(a)
</script></div>

<p>For some initial value problems, the 4th-order Runge-Kutta method used by desolve_system_rk4() is insufficient.  This will especially be the case when two or more variables are changing on very different time scales, in so-called 'stiff' systems.  You can use the versatile ode_solver() class, which can run an adaptive stepsize or implicit version of Euler's Method to successfully solve stiff systems.  Graphing the solution is also easier, with the convenient interpolate_solution() method:</p>

<div class="sagecell-plot" id="sagecell3"><script type="application/sage">
## Variables
var('mu t X S')

## Equations
mu = (S)/(1+S)
des = lambda t,y: [(y[1]/(1+y[1]))*y[0], -(y[1]/(1+y[1]))*y[0]]

T = ode_solver(function=des, y_0=[0.1,10])
T.ode_solve(t_span=[0,10], num_points=100)

a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='green', legend_label='[cells]')
a += list_plot(T.interpolate_solution(1),color='blue', legend_label='[substrate]')
a.axes_labels(['time','concentration'])
show(a)
</script></div>

<p>Now let's add some more detail to the model: the cellular growth rate 'mu' obeys saturation kinetics, attaining a value of 'mu_max' at high concentations of substrate 'S' and a value of 0.5*mu_max at substrate concentration 'K_m'; the conversion rate of substrate to cells is the growth yield, 'Y'.  Try modifying the values of mu_max, K_m, and Y and re-evaluating to see the effect!</p>

<div class="sagecell-plot" id="sagecell4"><script type="application/sage">
## Variables
var('t X S mu_max Y')
mu_max = 1.0; Y = 0.5; K_m = 2.0
## Equations
des = lambda t,y: [(mu_max*y[1]/(K_m+y[1]))*y[0], -(1/Y)*(mu_max*y[1]/(K_m+y[1]))*y[0]]
T = ode_solver(function=des, y_0=[0.1,10])
T.ode_solve(t_span=[0,10], num_points=100)

a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='green', legend_label='[cells]')
a += list_plot(T.interpolate_solution(1),color='blue', legend_label='[substrate]')
a.axes_labels(['time','concentration'])
show(a)
</script></div>

<h2><a id="cstr">The CSTR</a></h2>

<p>More complex still?  First let's simulate a CSTR.  The tank starts devoid of 'S', and at time=0 an input stream containing S is started:</p>

<div class="sagecell-plot" id="sagecell5"><script type="application/sage">
## Variables
var('t V V0 Vin Vout S S0 Sin')
V0 = 1; Vin = 0.1; Vout = 0.1; Sin = 10; S0=0
des = lambda t,y: [Vin-Vout, Sin*Vin/y[0]-y[1]*Vout/y[0]]
T = ode_solver(function=des, y_0=[V0,S0])
T.ode_solve(t_span=[0,100], num_points=100)
a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='grey', legend_label='volume')
a += list_plot(T.interpolate_solution(1),color='blue', legend_label='[substrate]')
a.axes_labels(['time',''])
show(a)
</script></div>

<p>How about if the system is fed batch, so Vin occurs in surges?</p>

<div class="sagecell-plot" id="sagecell6"><script type="application/sage">
## Variables
var('t V V0 Vout S S0 Sin')
V0 = 1; Sin = 10; S0=0
def Vin(t):
    if (t-floor(t)) <= 0.1: return 1
    else: return 0
def Vout(t): return Vin(t)

des = lambda t,y: [Vin(t)-Vout(t), Sin*Vin(t)/y[0]-y[1]*Vout(t)/y[0]]
T = ode_solver(function=des, y_0=[V0,S0])
T.ode_solve(t_span=[0,35], num_points=1000)
a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='grey', legend_label='volume')
a += list_plot(T.interpolate_solution(1),color='blue', legend_label='[substrate]')
a.axes_labels(['time',''])
show(a)
</script></div>

<h2><a id="cstr_rxn">CSTR with Reaction</a></h2>

<p>Now let's add a reaction, so that 'S' in the system is consumed with first-order kinetics:</p>

<div class="sagecell-plot" id="sagecell7"><script type="application/sage">
## Variables
var('t V V0 Vout S S0 Sin k')
V0 = 1; Sin = 10; S0=0; k = 0.025
def Vin(t):
    if (t-floor(t)) <= 0.1: return 1
    else: return 0
def Vout(t): return Vin(t)

des = lambda t,y: [Vin(t)-Vout(t), Sin*Vin(t)/y[0]-y[1]*Vout(t)/y[0]-k*y[1]]
T = ode_solver(function=des, y_0=[V0,S0])
T.ode_solve(t_span=[0,35], num_points=1000)
a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='grey', legend_label='volume')
a += list_plot(T.interpolate_solution(1),color='blue', legend_label='[substrate]')
a.axes_labels(['time',''])
show(a)
</script></div>

<p>How do things change if we switch from batch feed to continuous feed, intermittent effluent?</p>

<div class="sagecell-plot" id="sagecell8"><script type="application/sage">
## Variables
var('t V V0 Vout S S0 Sin k')
V0 = 1; Sin = 10; S0=0; k = 0.025
def Vin(t): return 0.1
def Vout(t):
    if (t-floor(t)) <= 0.1: return 1
    else: return 0
des = lambda t,y: [Vin(t)-Vout(t), Sin*Vin(t)-y[1]*Vout(t)/y[0]-k*y[1]/y[0]]
T = ode_solver(function=des, y_0=[V0,S0])
T.ode_solve(t_span=[0,35], num_points=1000)
solV = T.interpolate_solution(0); solS = T.interpolate_solution(1); sol = []
for i in range(0,len(T.solution)):
    sol.append([solS[i][0],solS[i][1]/solV[i][1]])
a = plot([],figsize=(6,4))
a += list_plot(solV,color='grey', legend_label='volume')
a += list_plot(sol,color='blue', legend_label='[substrate]')
a.axes_labels(['time','']); a.set_legend_options(loc=7); show(a)
</script></div>

<h2><a id="cstr_cells">CSTR with Cells</a></h2>

<p>Let's take the fully-continuous system and replace the first-order reaction with cell growth!</p>

<div class="sagecell-plot" id="sagecell9"><script type="application/sage">
var('t V V0 Vout S S0 Sin X X0 mu_max Y K_m')
V0 = 1; Sin = 10; S0=0; X0 = 0.1; mu_max = 1.0; Y = 0.5; K_m = 2.0
def Vin(t): return 0.1
def Vout(t): return 0.1
des = lambda t,y: [Vin(t)-Vout(t), Sin*Vin(t)-y[1]*Vout(t)/y[0]-(1/Y)*(mu_max*(y[1]/y[0])/(K_m+(y[1]/y[0])))*y[2], (mu_max*(y[1]/y[0])/(K_m+(y[1]/y[0])))*y[2]-Vout(t)*y[2]/y[0]]
T = ode_solver(function=des, y_0=[V0,S0,X0])
T.ode_solve(t_span=[0,35], num_points=1000)
solV = T.interpolate_solution(0); solS = T.interpolate_solution(1); solX = T.interpolate_solution(2); sol1 = []; sol2 = []
for i in range(0,len(T.solution)):
    sol1.append([solS[i][0],solS[i][1]/solV[i][1]])
    sol2.append([solX[i][0],solX[i][1]/solV[i][1]])
a = plot([],figsize=(6,4))
a += list_plot(solV,color='grey', legend_label='volume')
a += list_plot(sol1,color='blue', legend_label='[substrate]')
a += list_plot(sol2,color='green', legend_label='[cells]')
a.axes_labels(['time','']); a.set_legend_options(loc=7); show(a)
</script></div>

<p>Doing the same for the fed batch system, you see that this setup is not particularly chemostatic:</p>

<div class="sagecell-plot" id="sagecell10"><script type="application/sage">
var('t V V0 Vout S S0 Sin X X0 mu_max Y K_m')
V0 = 1; Sin = 10; S0=0; X0 = 0.1; mu_max = 1.0; Y = 0.5; K_m = 2.0
def Vin(t): 
    if (t-floor(t)) <= 0.1: return 1
    else: return 0
def Vout(t): return 0.1
des = lambda t,y: [Vin(t)-Vout(t), Sin*Vin(t)-y[1]*Vout(t)/y[0]-(1/Y)*(mu_max*(y[1]/y[0])/(K_m+(y[1]/y[0])))*y[2], (mu_max*(y[1]/y[0])/(K_m+(y[1]/y[0])))*y[2]-Vout(t)*y[2]/y[0]]
T = ode_solver(function=des, y_0=[V0,S0,X0])
T.ode_solve(t_span=[0,35], num_points=1000)
solV = T.interpolate_solution(0); solS = T.interpolate_solution(1); solX = T.interpolate_solution(2); sol1 = []; sol2 = []
for i in range(0,len(T.solution)):
    sol1.append([solS[i][0],solS[i][1]/solV[i][1]])
    sol2.append([solX[i][0],solX[i][1]/solV[i][1]])
a = plot([],figsize=(6,4))
a += list_plot(solV,color='grey', legend_label='volume')
a += list_plot(sol1,color='blue', legend_label='[substrate]')
a += list_plot(sol2,color='green', legend_label='[cells]')
a.axes_labels(['time','']); a.set_legend_options(loc=7); show(a)
</script></div>

<p>Unlike the fed-batch system, the intermittent effluent system is highly chemostatic:</p>

<div class="sagecell-plot" id="sagecell11"><script type="application/sage">
var('t V V0 Vout S S0 Sin X X0 mu_max Y K_m')
V0 = 1; Sin = 10; S0=0; X0 = 0.1; mu_max = 1.0; Y = 0.5; K_m = 2.0
def Vin(t): return 0.1
def Vout(t):
    if (t-floor(t)) <= 0.1: return 1
    else: return 0
des = lambda t,y: [Vin(t)-Vout(t), Sin*Vin(t)-y[1]*Vout(t)/y[0]-(1/Y)*(mu_max*(y[1]/y[0])/(K_m+(y[1]/y[0])))*y[2], (mu_max*(y[1]/y[0])/(K_m+(y[1]/y[0])))*y[2]-Vout(t)*y[2]/y[0]]
T = ode_solver(function=des, y_0=[V0,S0,X0])
T.ode_solve(t_span=[0,35], num_points=1000)
solV = T.interpolate_solution(0); solS = T.interpolate_solution(1); solX = T.interpolate_solution(2); sol1 = []; sol2 = []
for i in range(0,len(T.solution)):
    sol1.append([solS[i][0],solS[i][1]/solV[i][1]])
    sol2.append([solX[i][0],solX[i][1]/solV[i][1]])
a = plot([],figsize=(6,4))
a += list_plot(solV,color='grey', legend_label='volume')
a += list_plot(sol1,color='blue', legend_label='[substrate]')
a += list_plot(sol2,color='green', legend_label='[cells]')
a.axes_labels(['time','']); a.set_legend_options(loc=7); show(a)
</script></div>

<h2><a id="conical">Draining a Conical Vessel</a></h2>
<p>A conical vessel draining with controlled constant outflow rate:</p>

<div class="sagecell-plot" id="sagecell12"><script type="application/sage">
var('t V V0 Vout theta R H H0 k')
V0 = 1; k = 0.12; theta = pi()/4; H0 = (3*V0/(pi()*tan(theta)^2))^(1/3)
def Vin(t,H):
    if(H>=0): return 0.0
    else: return 0.0
def Vout(t,H):
    if(H>=0): return 0.1
    else: return 0.0
des = lambda t,y: [Vin(t,H=y[1])-Vout(t,H=y[1]), (1/(pi()*tan(theta)^2*y[1]^2))*(Vin(t,H=y[1])-Vout(t,H=y[1]))]
T = ode_solver(function=des, y_0=[V0,H0])
T.ode_solve(t_span=[0,10], num_points=100)
a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='lightgrey', legend_label='V, dV/dt=const')
a += list_plot(T.interpolate_solution(1),color='lightblue', legend_label='H, dV/dt=const')
a.axes_labels(['time',''])
show(a)
</script></div>

<p>A conical vessel draining with uncontrolled gravity flow, in which the outlet flow rate is proportional to the square root of the liquid height.  The variable 'k' is proportional to the square root the pressure drop across the orifice, which is itself proportional to fluid density; try changing this value to scale the magnitude of gravity's influence on the system.</p>

<div class="sagecell-plot" id="sagecell13"><script type="application/sage">
var('t V V0 Vout theta R H H0 k')
V0 = 1; k = 0.12; theta = pi()/4; H0 = (3*V0/(pi()*tan(theta)^2))^(1/3)
def Vin(t,H):
    if(H>=0): return 0.0
    else: return 0.0
def Vout(t,H):
    if(H>=0): return k*sqrt(H)
    else: return 0.0
des = lambda t,y: [Vin(t,H=y[1])-Vout(t,H=y[1]), (1/(pi()*tan(theta)^2*y[1]^2))*(Vin(t,H=y[1])-Vout(t,H=y[1]))]
T = ode_solver(function=des, y_0=[V0,H0])
T.ode_solve(t_span=[0,10], num_points=100)
a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='darkgrey', legend_label='V, dV/dt~sqrt(H)')
a += list_plot(T.interpolate_solution(1),color='darkblue', legend_label='H, dV/dt~sqrt(H)')
a.axes_labels(['time',''])
show(a)
</script></div>

<p>A conical vessel draining: controlled constant outflow and uncontrolled gravity flow, superimposed. Try changing 'k'!</p>

<div class="sagecell-plot" id="sagecell14"><script type="application/sage">
var('t V V0 Vout theta R H H0 k')
V0 = 1; k = 0.12; theta = pi()/4; H0 = (3*V0/(pi()*tan(theta)^2))^(1/3)

### Constant Controlled Outflow
def Vin(t,H):
    if(H>=0): return 0.0
    else: return 0.0
def Vout(t,H):
    if(H>=0): return 0.1
    else: return 0.0
des = lambda t,y: [Vin(t,H=y[1])-Vout(t,H=y[1]), (1/(pi()*tan(theta)^2*y[1]^2))*(Vin(t,H=y[1])-Vout(t,H=y[1]))]
T = ode_solver(function=des, y_0=[V0,H0])
T.ode_solve(t_span=[0,10], num_points=100)
a = plot([],figsize=(6,4))
a += list_plot(T.interpolate_solution(0),color='lightgrey', legend_label='V, dV/dt=const')
a += list_plot(T.interpolate_solution(1),color='lightblue', legend_label='H, dV/dt=const')
a.axes_labels(['time',''])

### Gravity Outflow, dV/dt ~ sqrt(H)
def Vin(t,H):
    if(H>=0): return 0.0
    else: return 0.0
def Vout(t,H):
    if(H>=0): return k*sqrt(H)
    else: return 0.0
des = lambda t,y: [Vin(t,H=y[1])-Vout(t,H=y[1]), (1/(pi()*tan(theta)^2*y[1]^2))*(Vin(t,H=y[1])-Vout(t,H=y[1]))]
T = ode_solver(function=des, y_0=[V0,H0])
T.ode_solve(t_span=[0,10], num_points=100)
a += list_plot(T.interpolate_solution(0),color='darkgrey', legend_label='V, dV/dt~sqrt(H)')
a += list_plot(T.interpolate_solution(1),color='darkblue', legend_label='H, dV/dt~sqrt(H)')
show(a)
</script></div>

<h2><a id="heat">Intercooled CSTR</a></h2>

<p>An immersion-intercooled CSTR. Try changing the convection coefficient 'h'!</p>

<div class="sagecell-plot" id="sagecell15"><script type="application/sage">
var('t T T0 Tci h A p C V Vc H0 dH')
T0 = 100; Tci = 15; h = 25; A = 0.61; p = 1; C = 4.186; V = 19; Vc = 5
H0 = 0.161+T0*p*C*V+0.5*4.5
# know that Tco = Tci + (-dH/dt)*(V/Vc)*(1/(p*C*V)); solve for dH/dt, where dH/dt = -h*A*LMTD
Tco = (Tci + (-dH)*(V/Vc)*(1/(p*C*V)))
f = (dH == -h*A*((T-Tco)-(T-Tci))/ln((T-Tco)/(T-Tci)))
dH(T) = solve(f,dH)[0].rhs()

# y[0] = H, y[1] = T; dT/dt = (1/p*C*V)*(dH/dt)
des = lambda t,y: [dH(T=y[1]), dH(T=y[1])*(1/(p*C*V))]
Q = ode_solver(function=des, y_0=[H0,T0])
Q.ode_solve(t_span=[0,25], num_points=100)

f = Q.interpolate_solution(0); g = []
for i in range(1,len(f)):
    g.append([f[i][0],Tci+(-((f[i][1]-f[i-1][1])/(f[i][0]-f[i-1][0]))*(V/Vc)*(1/(p*C*V)))])
    
a = plot([],figsize=(6,4))
a += list_plot(Q.interpolate_solution(1), color='blue', legend_label='dT/dt')
a += list_plot(g, color='grey', legend_label='Tco')
a.axes_labels(['time,min','temp, C']); a.set_axes_range(0,25,0); show(a)
</script></div>

<script type="text/javascript">sageFooter()</script>

</body>
</html>


